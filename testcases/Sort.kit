
class Sort{

	field int[] v
	field int lv

	constructor(){}
	
	constructor(int[] v, int lv){
		this.v := v;
		this.lv := lv
	}
	
	/* Metodo che trasforma l'array in una stringa */
	method String toString(){
	    String res := new String();
	        
	    for(int i:= 0 ; i < this.lv; i := i+1)
	  		res := res.concat(this.v[i]);
	  	
	  	
	  	return res.concat("\n")
  	}
  	
  	method void bubblesort(){
	  	int temp:=0;
	  	boolean scambio := true;
	  	
	  	while(scambio){
	  		scambio := false;
	  		
	  		for(int i:=0;i<this.lv-1;i := i+1){
	  			if(this.v[i] < this.v[i+1]) then{
	  				temp := this.v[i+1];
	  				this.v[i+1] := this.v[i];
	  				this.v[i] := temp;
	  				this.toString().output();
	  				scambio := true
	  			}
	  		}
	  	}
  	}
  	
  	
  	method void insertionsort(){
	  	
	  	for(int indice:=1; indice<this.lv; indice := indice+1){
	  		int j := indice;
	  		
	  		while(this.v[j]>this.v[j-1]){
	  			
	  			int temp := this.v[j-1];
	  			this.v[j-1] := this.v[j];
	  			this.v[j] := temp;
	  			
	  			this.toString().output();
	  			if(j>1) then j := j-1
	  		}
	  	}
  	}
  	
  	
  	method void mergesort(int left, int right){
  		int center:=0;
  		if(left<right) then{
  			center := (left +right)/2;
  			this.mergesort(left, center);
  			this.toString().output();
  			this.mergesort(center+1, right);
  			this.toString().output();
  			this.merge(left,center,right)
  		}
  	}
  	
  	method void merge(int left,int center ,int right){
  		int i:=left;
  		int j:= center +1;
  		int k:= left;
  		int[] b := new int[this.lv];
  		
  		while(i <= center & j <= right){
  			if(this.v[i] >= this.v[j]) then{
  			
  				b[k] := this.v[i];
  				i:=i+1
  			
  			}else{
  				b[k] := this.v[j];
  				j:=j+1
  			};
  			k:=k+1
  		};
  		
  		while(i <= center){
  			b[k] := this.v[i];
  			i := i+1;
  			k := k+1
  		};
  		
  		while(j <= right){
  			b[k] := this.v[j];
  			j := j+1;
  			k := k+1
  		};
  		
  		for(k:=left; k<=right; k := k+1)
  			this.v[k] := b[k]
  	
  	}
  	
  	  	
  	method void main(){
	    int larr := 9;
	    
	  	int[] arr := new int[larr];
	  	
	  	for(int i:=1; i<larr+1; i:= i+1){
	  		arr[i-1] := i
	  	};
	  	
	  	Sort objsort := new Sort(arr, larr);
	  	
	  	"".concat(objsort.toString()).output();
	  		  	
	  	objsort.mergesort(0,larr-1);
	  	
	  	"".concat(objsort.toString()).output()
	  	
	    
  }




}